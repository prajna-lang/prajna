struct String{
    darray: DynamicArray<char>;
}

implement operator::LinearIndex<char> for String {
    func Get(idx: i64)->char{
        return this.darray[idx];
    }

    func Set(idx: i64, value: char){
        this.darray[idx] = value;
    }
}

implement String{
    @static
    func Create(length: i64)->String{
        var str_re: String;
        str_re.darray = DynamicArray<char>::Create(length);
        return str_re;
    }

    func Length()->i64 {
        return this.darray.Length();
    }

    @static
    func __from_char_ptr(cstr_ptr: __ptr<char>)->String {
        var length = 0;
        while (cstr_ptr[length] != '\0'){
            length = length + 1;
        }
        var str_re = String::Create(length);
        for i in 0 to str_re.Length() {
            str_re.darray[i] = cstr_ptr[i];
        }

        return str_re;
    }

    func Print(){
        this.darray.Push('\0');
        bindings::print_c(this.darray._data.raw_ptr);
        this.darray.Pop();
    }

    func Push(v: char){
        this.darray.Push(v);
    }

    func Pop(){
        this.darray.Pop();
    }

    func PrintLine() {
        this.darray.Push('\n');
        this.Print();
        this.darray.Pop();
    }

    func ToString()->String{
        return this;
    }
}

implement operator::Add<String> for String {
    func Add(str_re: String)->String{
        var new_str = String::Create(this.Length() + str_re.Length());
        var i = 0;
        while (i < this.Length()){
            new_str.darray[i] = this.darray[i];
            i = i + 1;
        }
        var j = 0;
        while ( i < new_str.Length() ){
            new_str.darray[i] = str_re.darray[j];
            j = j + 1;
            i = i + 1;
        }

        return new_str;
    }
}

implement operator::Equal<String> for String{
    func Equal(rhs_str: String) -> bool{
        if(this.Length()!=rhs_str.Length()) {
            return false;
        }
        var i=0;
        while(i<this.Length()){
            if(rhs_str.darray[i]!=this.darray[i]) {
                return false;
            }
            i = i + 1;
        }
        return true;
    }
}

implement operator::NotEqual<String> for String{
    func NotEqual(rhs_str: String) -> bool{
         return !(this == rhs_str);
    }
}

func Print(ss: String){
    ss.Print();
}

func PrintLine(ss: String) {
    ss.PrintLine();
}

