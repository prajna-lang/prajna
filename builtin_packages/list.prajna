template <ValueType>
struct Node {
    next: ptr<Node<ValueType>>;
    prev: ptr<Node<ValueType>>;
    value: ValueType;
}

template <ValueType>
implement Node<ValueType> {
    /// @brief
    /// @warning 确保节点前面存在节点
    func InsertBefore(value: ValueType){
        var new_node = ptr<Node<ValueType>>::New();
        new_node->value = value;
        new_node->prev = this.prev;
        this.prev->next = new_node;
        new_node->next = &this;
        this.prev = new_node;
    }

    /// @warning 确保节点后面存在节点
    func InsertAfter(value: ValueType){
        var new_node = ptr<Node<ValueType>>::New();
        new_node->value = value;
        new_node->next = this.next;
        this.next->prev = new_node;
        new_node->prev = &this;
        this.next = new_node;
    }

    func RemoveBefore() {
        var prev_node = this.prev;
        var prev_prev_node = prev_node->prev;
        prev_prev_node->next = &this;
        this.prev = prev_prev_node;
        prev_node.Free();
    }

    func RemoveAfter() {
        var next_node = this.next;
        var next_next_node = next_node->next;
        this.next = next_next_node;
        next_next_node->prev = &this;
        next_node.Free();
    }
}

template <ValueType>
struct List {
    _head: ptr<Node<ValueType>>;
    _end: ptr<Node<ValueType>>;
    // 用于引用计数
    _reference_counter : ptr<i64>;
}

template <ValueType>
implement List<ValueType> {
    func PushFront(value: ValueType) {
        this._head->InsertAfter(value);
    }

    func PushBack(value: ValueType) {
        this._end->InsertBefore(value);
    }

    /// @brief 删除头节点
    /// @warning 确保链表非空
    func PopFront() {
        this._head->RemoveAfter();
    }

    /// @brief 删除尾节点
    /// @warning 确保链表非空
    func PopBack() {
        this._end->RemoveBefore();
    }

    func End()->ptr<Node<ValueType>> {
        return this._end;
    }

    func Begin()->ptr<Node<ValueType>> {
        return this._head->next;
    }

    func Length() -> i64 {
        var count = 0;
        var current = this.Begin();

        while (current != this.End()) {
            count = count + 1;
            current = current->next;
        }

        return count;
    }


    func __initialize__() {
        this._head = ptr<Node<ValueType>>::New();
        this._end = ptr<Node<ValueType>>::New();
        this._head->prev = ptr<Node<ValueType>>::Null();
        this._head->next = this._end;
        this._end->prev =  this._head;
        this._end->next = ptr<Node<ValueType>>::Null();

        this._reference_counter = ptr<i64>::New();
        *this._reference_counter = 1;
    }

    func __copy__() {
        *this._reference_counter = *this._reference_counter + 1;
    }

    func __finalize__() {
        *this._reference_counter = *this._reference_counter - 1;
        if (*this._reference_counter == 0){
            this._reference_counter.Free();

            var node = this.Begin();
            while (node != this.End()) {
                node->prev.Free();
                node = node->next;
            }
            this.End().Free();
        }
    }
}
