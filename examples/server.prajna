use net::_c;

func Main() {

    var server_fd = net::_c::socket(2i32, 1i32, 6i32);
    debug::Assert(server_fd >= 0i32);

    var server_addr = net::_c::create_socket_addr_in(0u8, 0u8, 0u8, 0u8, 8887u16);
    var server_addr_ptr = &server_addr;

    ("Server socket created: " + server_fd.ToString()).PrintLine();
    var bind_result = net::_c::bind(server_fd, server_addr_ptr, 16u32);
    debug::Assert(bind_result == 0i32);

    var listen_result = net::_c::listen(server_fd, 5i32); // backlog = 5
    debug::Assert(listen_result == 0i32);

    "Server listening on 0.0.0.0:8887".PrintLine();

    var client_addr: net::_c::socket_addr_in;
    var client_addr_ptr = &client_addr;
    var addr_len = 16u32;
    var addr_len_ptr = &addr_len;

    var client_fd = net::_c::accept(server_fd, client_addr_ptr, addr_len_ptr);
    debug::Assert(client_fd >= 0i32);

    ("Client connected! Client FD: " + client_fd.ToString()).PrintLine();

    // Receive data from client
    var receive_buffer: array<u8, 1024>;
    var receive_ptr = &receive_buffer[0];
    var bytes_received = net::_c::recv(client_fd, receive_ptr, 1024i64, 0i32);

    debug::Assert(bytes_received > 0i64);
    "Client message (received raw bytes):".PrintLine();
    var i = 0i64;
    var line = "";
    while (i < bytes_received && i < 30i64) {
        line = line + receive_buffer[i].ToString();
        i = i + 1i64;
        if (i < bytes_received && i < 30i64) {
            line = line + ", ";
        }
    }
    line.PrintLine();

    var response_buffer: array<u8, 1024> = receive_buffer;
    var response_ptr = &response_buffer[0];
    var response_len = bytes_received; // Echo back the same length
    var bytes_sent = net::_c::send(client_fd, response_ptr, response_len, 0i32);
    debug::Assert(bytes_sent > 0i64);

    // Close client connection
    var close_client_result = net::_c::close(client_fd);
    debug::Assert(close_client_result == 0i32);

    // Close server socket
    var close_server_result = net::_c::close(server_fd);
    debug::Assert(close_server_result == 0i32);
}